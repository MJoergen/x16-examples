100 REM THIS PROGRAM SOLVES THE N-QUEENS PUZZLE.
110 REM THE GOAL IS TO PLACE N QUEENS
120 REM  ON A SQUARE BOARD, SUCH THAT NO TWO
130 REM  QUEENS ATTACK EACH OTHER.
140 REM ONE OF THE TWO SOLUTIONS FOR N=4 IS:
150 REM   . Q . .
160 REM   . . . Q
170 REM   Q . . .
180 REM   . . Q .

200 REM FOR N=8 THERE ARE 92 SOLUTIONS,
210 REM  WHICH ARE FOUND IN APPROX 3 MINUTES.

300 REM THE NUMBER OF QUEENS IS IN THE VARIABLE N.
310 REM THE CURRENT QUEEN BEING CONSIDERED
320 REM  IS IN THE VARIABLE I.
330 REM THE ALGORITHM IS A RECURSIVE DEPTH FIRST.

400 REM INITIALIZE THE VARIABLES AND PREPARE FOR RECURSION.
410 N=7
430 DIM S(N-1)
440 I=0

500 REM INITIALIZE SCREEN
510 PRINT CHR$(147)
520 FOR C=0 TO N-1
530 FOR D=0 TO N-1
540 VPOKE 0, D*256+C*2+1, 2*16
550 NEXT D
560 PRINT
570 NEXT C

600 REM START RECURSION
610 P=TI
620 GOSUB 1100
630 Q=TI
640 PRINT "TIME=";(Q-P)/60;"SECONDS"
650 END

1000 REM THE RECURSIVE LOOP.
1010 REM ON ENTRY, THE ARRAY S() CONTAINS
1020 REM  POSITIONS OF EACH QUEEN SO FAR.
1030 REM ON ENTRY, THE VARIBLE I CONTAINS
1040 REM  THE NUMBER OF QUEENS ALREADY PLACED.

1100 IF I>=N THEN GOTO 2000

1110 REM CLEAR START OF THIS QUEEN
1120 J=0

1200 REM HAVE WE RUN OUT OF PLACES?
1210 IF J>=N THEN RETURN
1220 REM CAN QUEEN BE PLACED IN (I,J)?
1230 K=0
1240 IF K >= I GOTO 1400
1250 IF S(K) = J         GOTO 1700
1260 IF S(K) = J + (K-I) GOTO 1700
1270 IF S(K) = J + (I-K) GOTO 1700
1280 K=K+1
1290 GOTO 1240

1400 REM POPULATE QUEEN INTO ARRAY
1410 S(I)=J
1420 VPOKE 0, I*256+J*2+1, 1*16

1500 REM RECURSIVELY PLACE THE NEXT QUEEN
1510 I=I+1
1520 GOSUB 1100
1530 I=I-1

1600 REM UNDO PLACEMENT
1610 J=S(I)
1620 VPOKE 0, I*256+J*2+1, 2*16

1700 REM SEARCH REMAINING POSITIONS FOR THIS PAIR
1710 J=J+1
1720 GOTO 1200

2000 REM PRINT OUT A SOLUTION
2010 R=R+1
2020 PRINT "SOLUTION";R,
2030 FOR K=0 TO N-1
2040 PRINT S(K);
2050 NEXT K
2060 PRINT
2070 RETURN

