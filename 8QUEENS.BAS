10 REM THIS PROGRAM SOLVES THE N-QUEENS PUZZLE.
20 REM THE GOAL IS TO PLACE N QUEENS
30 REM  ON A SQUARE BOARD, SUCH THAT NO TWO
40 REM  QUEENS ATTACK EACH OTHER.
100 REM ONE OF THE TWO SOLUTIONS FOR N=4 IS:
110 REM   . Q . .
120 REM   . . . Q
130 REM   Q . . .
140 REM   . . Q .

150 REM FOR N=8 THERE ARE 92 SOLUTIONS,
160 REM  WHICH ARE FOUND IN APPROX 3 MINUTES.

200 REM THE NUMBER OF QUEENS IS IN THE VARIABLE N.
210 REM THE CURRENT QUEEN BEING CONSIDERED
220 REM  IS IN THE VARIABLE I.
230 REM THE ALGORITHM IS A RECURSIVE DEPTH FIRST.

300 REM INITIALIZE THE VARIABLE AND PREPARE FOR RECURSION.
310 N=8
330 DIM S(N-1)
340 I=0

400 REM START RECURSION
410 P=TI
420 GOSUB 1100
430 Q=TI
440 PRINT "TIME=";(Q-P)/60;"SECONDS"
450 END

1000 REM THE RECURSIVE LOOP.
1010 REM ON ENTRY, THE ARRAY S() CONTAINS
1020 REM  POSITIONS OF EACH QUEEN SO FAR.
1030 REM ON ENTRY, THE VARIBLE I CONTAINS
1040 REM  THE NUMBER OF QUEENS ALREADY PLACED.

1100 IF I>=N THEN GOTO 2000

1110 REM CLEAR START OF THIS QUEEN
1120 J=0

1200 REM HAVE WE RUN OUT OF PLACES?
1210 IF J>=N THEN RETURN
1220 REM CAN QUEEN BE PLACED IN (I,J)?
1230 K=0
1240 IF K >= I GOTO 1400
1250 IF S(K) = J GOTO 1600
1260 IF S(K) = J + (K-I) GOTO 1600
1270 IF S(K) = J + (I-K) GOTO 1600
1280 K=K+1
1290 GOTO 1240

1400 REM POPULATE QUEEN INTO ARRAY
1410 S(I)=J
1420 REM RECURSIVELY PLACE THE NEXT QUEEN
1430 I=I+1
1440 GOSUB 1100
1450 I=I-1
1460 REM UNDO PLACEMENT
1470 J=S(I)

1600 REM SEARCH REMAINING POSITIONS FOR THIS PAIR
1610 J=J+1
1620 GOTO 1200

2000 REM PRINT OUT A SOLUTION
2010 R=R+1
2020 PRINT "SOLUTION";R,
2030 FOR K=0 TO N-1
2040 PRINT S(K);
2050 NEXT K
2060 PRINT
2070 RETURN

